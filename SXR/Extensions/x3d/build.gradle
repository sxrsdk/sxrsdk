apply plugin: 'com.android.library'

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs '../../sxr-libs'
        }
    }
}

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

project.ext.jomlVersion = "1.9.3-SNAPSHOT"
project.ext.gsonVersion = '2.8.2'

repositories {
    jcenter()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.joml:joml-android:${jomlVersion}"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation 'com.google.android.exoplayer:exoplayer:2.6.0'
    if (findProject(':sxrsdk')) {
        implementation project (':sxrsdk')
    } else {
        implementation(name: 'sxrsdk-debug', ext: 'aar')
    }
    if (findProject(':script')) {
        implementation project (':script')
    } else {
        implementation(name: 'script-debug', ext: 'aar')
    }
}


clean {}.doLast {
    def sxrLibs = project.rootDir.absolutePath + "/../sxr-libs"
    if (file(sxrLibs).exists()) {
        project.delete(sxrLibs + '/' + project.name + '-release.aar')
        project.delete(sxrLibs + '/' + project.name + '-debug.aar')
    }
}

task copyJarToBin(type: Copy) {
    outputs.upToDateWhen { false }
    from tasks.matching { it.name.startsWith('bundle') }
    into project.rootDir.absolutePath + "/../sxr-libs"
}

tasks.whenTaskAdded { task ->
    if (task.name == 'bundleDebugAar') {
        task.finalizedBy(copyJarToBin)
    } else if (task.name == 'bundleReleaseAar') {
        task.finalizedBy(copyJarToBin)
    }
}
