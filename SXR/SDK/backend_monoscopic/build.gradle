apply plugin: 'com.android.library'

android {
    compileSdkVersion 24

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26

        externalNativeBuild {
            ndkBuild {
                arguments = ["-j" + Runtime.runtime.availableProcessors()]
                arguments += ['PROJECT_ROOT=' + rootProject.projectDir]
                arguments += ['PROJECT_DIR=' + projectDir.absolutePath]
                if (rootProject.hasProperty("ARM64")) {
                    arguments += ['ARM64=true']
                }
            }
        }

        ndk {
            moduleName "sxr-monoscopic"
            if (rootProject.hasProperty("SXRSDK_ABI_FILTER")) {
                abiFilters rootProject.property("SXRSDK_ABI_FILTER")
            } else {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }

    task buildInfo() {
        def commitId

        File head = new File(projectDir.absolutePath + '/../../../.git/HEAD')
        if (head.exists()) {
            def elements = head.text.split()
            if (elements.length > 0) {
                File commit = new File(projectDir.absolutePath + '/../../../.git/' + elements[elements.length-1])
                if (commit.exists()) {
                    commitId = commit.text.trim();
                } else {
                    return
                }
            } else {
                return
            }
        } else {
            return
        }

        def source = new File(projectDir.absolutePath + '/src/main/jni', 'buildinfo.cpp')
        if (source.exists() && source.text.contains(commitId)) {
            return
        }
        def javaSource = new File(projectDir.absolutePath + '/src/main/java/com/samsungxr/', 'MonoscopicBuildInfo.java')

        source.text = '#include "util/sxr_log.h"\n'
        source.text += '#include <jni.h>\n\n'
        source.text += 'jint JNI_OnLoad(JavaVM *vm, void *reserved) {\n'
        source.text += '    LOGI("BACKEND_MONOSCOPIC-CPP  HEAD: ' + commitId + '");\n'
        source.text += '    JNIEnv* env;\n';
        source.text += '    if(JNI_OK == vm->AttachCurrentThread(&env, 0)) {\n'
        source.text += '        jclass java_class = env->FindClass("com/samsungxr/OvrBuildInfo");\n'
        source.text += '        if (env->ExceptionCheck()) {\n'
        source.text += '            env->ExceptionClear();\n'
        source.text += '        }\n'
        source.text += '        if(nullptr != java_class) {\n'
        source.text += '            env->GetStaticFieldID(java_class, "HEAD", "java/lang/String");\n'
        source.text += '            if (env->ExceptionCheck()) {\n'
        source.text += '                env->ExceptionClear();\n'
        source.text += '            }\n'
        source.text += '            env->DeleteLocalRef(java_class);\n'
        source.text += '        }\n'
        source.text += '    }\n'
        source.text += '    return JNI_VERSION_1_6;\n'
        source.text += '}'

        javaSource.text = 'package com.samsungxr;\n'
        javaSource.text += 'public class MonoscopicBuildInfo {\n'
        javaSource.text += '    public final static String HEAD="' + commitId + '";\n'
        javaSource.text += '    static { android.util.Log.i("sxr", "BACKEND_MONOSCOPIC-JAVA HEAD: " + HEAD); }\n'
        javaSource.text += '}\n'
    }

    buildTypes {
        debug {
            debuggable = true
            jniDebuggable = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    sourceSets.main {
        java.srcDirs = ['src/main/java']
        jni.srcDirs = [] // no auto generation of Android.mk
        // pre-compiled libraries
        jniLibs {
            srcDir 'src/main/libs'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    packagingOptions {
        exclude 'lib/*/libsxrsdk.so'
    }
}

project.ext.jomlVersion = "1.9.3-SNAPSHOT"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(include: ['*.jar'], dir: 'src/main/libs')
    implementation project(':sxrsdk')
    implementation "org.joml:joml-android:${jomlVersion}"
}

clean {}.doLast {
    def sxrLibs = project.rootDir.absolutePath + "/../sxr-libs"
    if (file(sxrLibs).exists()) {
        project.delete(sxrLibs + '/' + project.name + '-release.aar')
        project.delete(sxrLibs + '/' + project.name + '-debug.aar')
    }
}

task copyJarToBin(type: Copy) {
    outputs.upToDateWhen { false }
    from tasks.matching { it.name.startsWith('bundle') }
    into project.rootDir.absolutePath + "/../sxr-libs"
}